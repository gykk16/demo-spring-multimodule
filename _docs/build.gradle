import java.time.LocalDate

plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

bootJar { enabled = true }
jar { enabled = true }

// ================================================================================================================
// application.yml 읽어오기
// ================================================================================================================
def documentVer = LocalDate.now()

group 'kr.co.smilecon'
version = documentVer

configurations {
    asciidoctorExt
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {

    // spring
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // spring rest docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    baseDirFollowsSourceFile()

    dependsOn test
}

bootJar {
    dependsOn asciidoctor

    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}

tasks.register('copyDocument', Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

build {
    dependsOn copyDocument
}